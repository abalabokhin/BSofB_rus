BEGIN L#RVFARM

//INTRO

CHAIN IF ~Global("L#RVMainQuest","GLOBAL",1)~ THEN L#RVFARM L#RVFARM.01.00
@0
END
IF~~THEN REPLY @1 EXTERN L#RVFARM L#RVFARM.01.01
IF~~THEN REPLY @2 EXTERN L#RVFARM L#RVFARM.01.02
IF~~THEN REPLY @3 EXTERN L#RVFARM L#RVFARM.01.03

CHAIN L#RVFARM L#RVFARM.01.01
@4
EXTERN L#RVFARM L#RVFARM.01.04

CHAIN L#RVFARM L#RVFARM.01.02
@5
EXTERN L#RVFARM L#RVFARM.01.04

CHAIN L#RVFARM L#RVFARM.01.03
@6
EXTERN L#RVFARM L#RVFARM.01.04

CHAIN L#RVFARM L#RVFARM.01.04
@7
==L#RVFARM @8
==YOSHJ IF ~InParty("yoshimo") !StateCheck("yoshimo",CD_STATE_NOTVALID)~ THEN @9
==NALIAJ IF ~InParty("nalia") !StateCheck("nalia",CD_STATE_NOTVALID)~ THEN @10
==JAHEIRAJ IF ~InParty("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @11
==CERNDJ IF ~InParty("cernd") !StateCheck("cernd",CD_STATE_NOTVALID)~ THEN @12
==L#RVFARM @13
END
IF~~THEN REPLY @14 EXTERN L#RVFARM L#RVFARM.01.05
IF~~THEN REPLY @15 EXTERN L#RVFARM L#RVFARM.01.05
IF~~THEN REPLY @16 EXTERN L#RVFARM L#RVFARM.01.05

CHAIN L#RVFARM L#RVFARM.01.05
@17
==JANJ IF ~InParty("jan") !StateCheck("jan",CD_STATE_NOTVALID)~ THEN @18
==L#RVFARM @19
==L#RVFARM @20
END
IF~~THEN REPLY @21 EXTERN L#RVFARM L#RVFARM.01.06
IF~~THEN REPLY @22 EXTERN L#RVFARM L#RVFARM.01.06

CHAIN L#RVFARM L#RVFARM.01.06
@23
==L#RVFARM @24
==L#RVFARM @25
==ANOMENJ IF ~InParty("anomen") !StateCheck("anomen",CD_STATE_NOTVALID)~ THEN @26
==VALYGARJ IF ~InParty("valygar") !StateCheck("valygar",CD_STATE_NOTVALID)~ THEN @27
==L#RVFARM @28
END
IF~~THEN REPLY @29 EXTERN L#RVFARM L#RVFARM.01.07
IF~~THEN REPLY @30 EXTERN L#RVFARM L#RVFARM.01.08

CHAIN L#RVFARM L#RVFARM.01.07
@31
END
IF~~THEN REPLY @32 EXTERN L#RVFARM L#RVFARM.01.09
IF~~THEN REPLY @33 EXTERN L#RVFARM L#RVFARM.01.10

CHAIN L#RVFARM L#RVFARM.01.08
@34
==L#RVFARM @35
END
IF~~THEN REPLY @36 EXTERN L#RVFARM L#RVFARM.01.09
IF~~THEN REPLY @37 EXTERN L#RVFARM L#RVFARM.01.10

CHAIN L#RVFARM L#RVFARM.01.09
@38
EXTERN L#RVFARM L#RVFARM.01.11

CHAIN L#RVFARM L#RVFARM.01.10
@39
EXTERN L#RVFARM L#RVFARM.01.11

CHAIN L#RVFARM L#RVFARM.01.11
@40
==YOSHJ IF ~InParty("yoshimo") !StateCheck("yoshimo",CD_STATE_NOTVALID)~ THEN @41
==EDWINJ IF ~InParty("edwin") !StateCheck("edwin",CD_STATE_NOTVALID)~ THEN @42
==L#RVFARM @43
==RASAADJ IF ~InParty("rasaad") !StateCheck("rasaad",CD_STATE_NOTVALID)~ THEN @44
==L#RVFARM @45
==L#RVFARM @46
END
IF~~THEN REPLY @47 EXTERN L#RVFARM L#RVFARM.01.12
IF~~THEN REPLY @48 EXTERN L#RVFARM L#RVFARM.01.12
IF~~THEN REPLY @49 EXTERN L#RVFARM L#RVFARM.01.13

CHAIN L#RVFARM L#RVFARM.01.12
@50
EXTERN L#RVFARM L#RVFARM.01.14

CHAIN L#RVFARM L#RVFARM.01.13
@51
EXTERN L#RVFARM L#RVFARM.01.14

CHAIN L#RVFARM L#RVFARM.01.14
@52
==L#RVFARM @53
==L#RVFARM @54
DO ~SetGlobal("L#RVMainQuest","GLOBAL",2) CreateCreature("L#RVSWAR",[980.1410],0) CreateCreature("L#RVSWAR",[800.1440],0) CreateCreature("L#RVSWAR",[500.1500],0)~ EXIT

//FIGHT

CHAIN IF ~GlobalGT("L#RVMainQuest","GLOBAL",0) OR(2) See([ENEMY]) CombatCounterGT(0)~ THEN L#RVFARM L#RVFARM.0F.00
@55
EXIT

//AFTER FIRST

CHAIN IF ~Global("L#RVMainQuest","GLOBAL",2)~ THEN L#RVFARM L#RVFARM.02.00
@56
END
IF~Global("L#RVRopeInformation","GLOBAL",1) Global("L#RVGiveKeyNowFarmer","GLOBAL",0)~THEN REPLY @57 EXTERN L#RVFARM L#RVFARM.02.01
IF~~THEN REPLY @58 EXTERN L#RVFARM L#RVFARM.02.02
IF~PartyHasItem("L#RVMKEY")~THEN REPLY @59 EXTERN L#RVFARM L#RVFARM.02.03
IF~Dead("L#RVQUEE") Global("L#RVInformedQueenDead","GLOBAL",0)~THEN REPLY @60 DO ~SetGlobal("L#RVInformedQueenDead","GLOBAL",1)~ EXTERN L#RVFARM L#RVFARM.02.04
IF~GlobalGT("L#RVKingIsDead","GLOBAL",0) Global("L#RVInformedQueenDead","GLOBAL",1)~THEN REPLY @61 EXTERN L#RVFARM L#RVFARM.02.06
IF~~THEN REPLY @62 EXIT

CHAIN L#RVFARM L#RVFARM.02.01
@63
==L#RVFARM @64
DO ~SetGlobal("L#RVGiveKeyNowFarmer","GLOBAL",1) GiveItemCreate("L#RVCHKE",Player1,1,0,0)~ EXIT

CHAIN L#RVFARM L#RVFARM.02.02
@65
==L#RVFARM @66
EXIT

CHAIN L#RVFARM L#RVFARM.02.03
@67
EXIT

CHAIN L#RVFARM L#RVFARM.02.04
@68
END
IF~~THEN REPLY @69 EXTERN L#RVFARM L#RVFARM.02.04x
IF~~THEN REPLY @70 EXTERN L#RVFARM L#RVFARM.02.04x

CHAIN L#RVFARM L#RVFARM.02.04x
@71
==L#RVFARM @72
END
IF~GlobalGT("L#RVKingIsDead","GLOBAL",0)~THEN REPLY @73 EXTERN L#RVFARM L#RVFARM.02.06
IF~GlobalGT("L#RVKingIsDead","GLOBAL",0)~THEN REPLY @74 EXTERN L#RVFARM L#RVFARM.02.06
IF~Global("L#RVKingIsDead","GLOBAL",0)~THEN REPLY @75 EXTERN L#RVFARM L#RVFARM.02.05
IF~Global("L#RVKingIsDead","GLOBAL",0)~THEN REPLY @76 EXTERN L#RVFARM L#RVFARM.02.05b

CHAIN L#RVFARM L#RVFARM.02.06
@77
DO ~AddExperienceParty(50000) GiveGoldForce(1000) GiveItemCreate("misc41",Player1,1,0,0) GiveItemCreate("misc40",Player1,3,0,0) SetGlobal("L#RVMainQuest","GLOBAL",3)~ 
==L#RVFARM @78
==L#RVFARM @79
EXTERN L#RVFARM L#RVFARM.03.00

CHAIN L#RVFARM L#RVFARM.02.05
@80
EXIT

CHAIN L#RVFARM L#RVFARM.02.05b
@81
EXIT

//AFTER MAIN ENEMIES ARE DEAD

CHAIN IF ~Global("L#RVMainQuest","GLOBAL",3)~ THEN L#RVFARM L#RVFARM.03.00
@82
END
IF~Global("L#RVSeenTheList","GLOBAL",1) !PartyHasItem("L#RVCOM1")~THEN REPLY @83 EXTERN L#RVFARM L#RVFARM.03.01
IF~Global("L#RVSeenTheList","GLOBAL",1) !PartyHasItem("L#RVCOM2")~THEN REPLY @84 EXTERN L#RVFARM L#RVFARM.03.02
IF~Global("L#RVSeenTheList","GLOBAL",1) !PartyHasItem("L#RVCOM3")~THEN REPLY @85 EXTERN L#RVFARM L#RVFARM.03.03
IF~Global("L#RVSeenTheList","GLOBAL",1) !PartyHasItem("L#RVCOM4")~THEN REPLY @86 EXTERN L#RVFARM L#RVFARM.03.04
IF~Global("L#RVSeenTheList","GLOBAL",1) !PartyHasItem("L#RVCOM5")~THEN REPLY @87 EXTERN L#RVFARM L#RVFARM.03.05
IF~~THEN REPLY @88 EXIT

CHAIN L#RVFARM L#RVFARM.03.01
@89
EXTERN L#RVFARM L#RVFARM.03.00

CHAIN L#RVFARM L#RVFARM.03.02
@90
==L#RVFARM @91
EXTERN L#RVFARM L#RVFARM.03.00

CHAIN L#RVFARM L#RVFARM.03.03
@92
==L#RVFARM @93
DO ~TriggerActivation("L#rvtotree",TRUE)~ 
==L#RVFARM @94
EXTERN L#RVFARM L#RVFARM.03.00

CHAIN L#RVFARM L#RVFARM.03.04
@95
EXTERN L#RVFARM L#RVFARM.03.00

CHAIN L#RVFARM L#RVFARM.03.05
@96
EXTERN L#RVFARM L#RVFARM.03.00

