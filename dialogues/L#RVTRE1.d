BEGIN L#RVTRE1

//WOKE UP

CHAIN IF ~Global("L#RVFeedTheTree","GLOBAL",0)~ THEN L#RVTRE1 START000
@0
END
IF~~THEN REPLY @1 DO ~SetGlobal("L#RVFeedTheTree","GLOBAL",1)~ EXTERN L#RVTRE1 START001
IF~~THEN REPLY @2 DO ~SetGlobal("L#RVFeedTheTree","GLOBAL",1)~ EXTERN L#RVTRE1 START002
IF~~THEN REPLY @3 DO ~SetGlobal("L#RVFeedTheTree","GLOBAL",1)~ EXTERN L#RVTRE1 START003

CHAIN L#RVTRE1 START001
@4
EXTERN L#RVTRE1 START004

CHAIN L#RVTRE1 START002
@5
EXTERN L#RVTRE1 START004

CHAIN L#RVTRE1 START003
@6
EXTERN L#RVTRE1 START004

CHAIN L#RVTRE1 START004
@7
==L#RVTRE1 @8
==L#RVTRE1 @9
==L#RVTRE1 @10
==L#RVTRE1 @11
EXTERN L#RVTRE1 NORMAL00.00

CHAIN IF ~Global("L#RVFeedTheTree","GLOBAL",1)~ THEN L#RVTRE1 NORMAL00.00 // appears after the previous line and when you start a new talk
@12
END
IF~PartyHasItem("L#RVHAIR") !Global("L#RVTreeCounter","GLOBAL",3)~THEN REPLY @13 EXTERN L#RVTRE1 NORMAL00.Q1
IF~PartyHasItem("L#RVLET1") !Global("L#RVTreeCounter","GLOBAL",3)~THEN REPLY @14 EXTERN L#RVTRE1 NORMAL00.Q2
IF~PartyHasItem("L#RVLOCK") !Global("L#RVTreeCounter","GLOBAL",3)~THEN REPLY @15 EXTERN L#RVTRE1 NORMAL00.Q3
IF~~THEN REPLY @16 EXTERN L#RVTRE1 NORMAL00.01
IF~~THEN REPLY @17 EXTERN L#RVTRE1 NORMAL00.02
IF~~THEN REPLY @18 EXTERN L#RVTRE1 NORMAL00.03
IF~~THEN REPLY @19 EXIT

CHAIN L#RVTRE1 NORMAL00.01
@20
==L#RVTRE1 @21
EXTERN L#RVTRE1 NORMAL00.00

CHAIN L#RVTRE1 NORMAL00.02
@22
EXTERN L#RVTRE1 NORMAL00.00

CHAIN L#RVTRE1 NORMAL00.03
@23
EXTERN L#RVTRE1 NORMAL00.00

CHAIN L#RVTRE1 NORMAL00.Q1
@24
DO ~TakePartyItem("L#RVHAIR") DestroyItem("L#RVHAIR") IncrementGlobal("L#RVTreeCounter","GLOBAL",1)~
==L#RVTRE1 @25
==L#RVTRE1 @26
==L#RVTRE1 @27
DO ~AddExperienceParty(5000)~
==L#RVTRE1 @28
==L#RVTRE1 @29
EXTERN L#RVTRE1 NORMAL00.QK

CHAIN L#RVTRE1 NORMAL00.Q2
@30
DO ~TakePartyItem("L#RVLET1") DestroyItem("L#RVLET1") IncrementGlobal("L#RVTreeCounter","GLOBAL",1)~
==L#RVTRE1 @31
==L#RVTRE1 @32
DO ~AddExperienceParty(5000)~
==L#RVTRE1 @33
EXTERN L#RVTRE1 NORMAL00.QK

CHAIN L#RVTRE1 NORMAL00.Q3
@34
DO ~TakePartyItem("L#RVLOCK") DestroyItem("L#RVLOCK") IncrementGlobal("L#RVTreeCounter","GLOBAL",1)~
==L#RVTRE1 @35
==L#RVTRE1 @36
==L#RVTRE1 @37
DO ~AddExperienceParty(5000)~
==L#RVTRE1 @38
EXTERN L#RVTRE1 NORMAL00.QK

CHAIN L#RVTRE1 NORMAL00.QK
@39
END
IF~!Global("L#RVTreeCounter","GLOBAL",3)~THEN EXTERN L#RVTRE1 NORMAL00.00
IF~Global("L#RVTreeCounter","GLOBAL",3)~THEN EXTERN L#RVTRE1 NORMAL00.QK1

CHAIN L#RVTRE1 NORMAL00.QK1
@40
==L#RVTRE1 @41
==L#RVTRE1 @42
DO ~SetGlobal("L#RVFeedTheTree","GLOBAL",2) GiveItemCreate("L#RVRSTA",Player1,1,1,0) AddExperienceParty(15000) CreateVisualEffect("SPCMDSI",[2400.330]) TriggerActivation("Treetrigger2",FALSE)~ EXIT

//FED (No Ring)

CHAIN IF ~Global("L#RVFeedTheTree","GLOBAL",2) !PartyHasItem("L#RVWEDB")~ THEN L#RVTRE1 FED00
@43
EXIT

//FED (HAS RING)

CHAIN IF ~Global("L#RVFeedTheTree","GLOBAL",2) PartyHasItem("L#RVWEDB")~ THEN L#RVTRE1 FED0R.00
@44
END
IF~~THEN REPLY @45 EXTERN L#RVTRE1 FED0R.01
IF~~THEN REPLY @46 DO ~SetGlobal("L#RVFeedTheTree","GLOBAL",3) TriggerActivation("Treetrigger2",FALSE)~ EXIT

CHAIN L#RVTRE1 FED0R.01
@47
==L#RVTRE1 @48
==L#RVTRE1 @49
==L#RVTRE1 @50
END
IF~~THEN REPLY @51 EXTERN L#RVTRE1 FED0R.02
IF~~THEN REPLY @52 EXTERN L#RVTRE1 FED0R.03

CHAIN L#RVTRE1 FED0R.02
@53
==L#RVTRE1 @54
DO ~AddexperienceParty(7500) SetGlobal("L#RVFeedTheTree","GLOBAL",3) TriggerActivation("Treetrigger2",FALSE)~ EXIT

CHAIN L#RVTRE1 FED0R.03
@55
==L#RVTRE1 @56
==L#RVTRE1 @57
DO ~AddexperienceParty(7500) TakePartyItem("L#RVWEDB") DestroyItem("L#RVWEDB") SetGlobal("L#RVFeedTheTree","GLOBAL",3) TriggerActivation("Treetrigger2",FALSE)~ EXIT



